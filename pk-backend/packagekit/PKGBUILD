# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=packagekit
pkgver=0.6.4
pkgrel=1
pkgdesc='A system designed to make installing and updating software on your computer easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('glib2>=2.22.0' 'dbus-glib>=0.74' 'networkmanager>=0.6.4'
         'polkit>=0.92' 'pacman-glib>=3.3.0' 'python>=2.6' 'sqlite3')
makedepends=('intltool' 'pm-utils' 'gobject-introspection')
backup=('var/lib/PackageKit/transactions.db'
        'etc/PackageKit/pacman.d/pacman.conf'
        'etc/PackageKit/pacman.d/repos.list')
options=('!libtool' '!strip')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2"
        'pacman-glib.patch')
sha256sums=('8112808350dc5f26f2216bdad779e480d61e0744c6f827710e6f6130d985884d'
            '4dd0b96e0091c4717d6e53b6eb8dbea6a5f92ac2b361f5e6a0e9f30367ca7174')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	patch -Np1 -i "${srcdir}/pacman-glib.patch" || return 1
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in || return 1
	
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-qt \
		--disable-managed \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-dummy \
		--enable-pacman \
		--with-default-backend=pacman || return 1
	
	make CFLAGS='-g -O0' -s || return 1
	make -s DESTDIR="${pkgdir}" install || return 1
	
	# clean up unneeded stuff
	rm -rf "${pkgdir}/usr/share/PackageKit/website"
	
	# rename bash completion script
	mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"
	
	# make sure log gets removed
	touch "${pkgdir}/var/log/PackageKit"
}
