# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=packagekit
pkgver=0.6.1
pkgrel=1
pkgdesc='A system designed to make installing and updating software on your computer easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib>=0.74' 'polkit>=0.92' 'networkmanager>=0.6.4' 'gpacman>=3.3.0' 'libtar' 'python>=2.6' 'sqlite3')
makedepends=('intltool' 'pm-utils' 'gobject-introspection')
backup=('var/lib/PackageKit/transactions.db')
options=('!libtool')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2"
        'gpacman.patch')
md5sums=('d3835290e88e534a60ca18bd4efa0479'
         '33203c911038ec6911a56362ed06b593')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	patch -p1 -i "${srcdir}/gpacman.patch" || return 1
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in || return 1
	
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-qt \
		--disable-managed \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-ruck \
		--disable-dummy \
		--enable-alpm \
		--with-default-backend=alpm || return 1
	
	make -s || return 1
	make -s DESTDIR="${pkgdir}" install || return 1
	
	# clean up unneeded stuff
	rm -rf "${pkgdir}/usr/share/PackageKit/website"
	
	# rename bash completion script
	mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"
}
