# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=packagekit
pkgver=0.6.6
pkgrel=1
pkgdesc='A system designed to make installing and updating software on your computer easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('glib2>=2.22.0' 'dbus-glib>=0.74' 'networkmanager>=0.6.4'
         'polkit>=0.92' 'pacman-glib>=3.4.0' 'python>=2.6' 'sqlite3')
makedepends=('intltool' 'pm-utils' 'gobject-introspection')
backup=('var/lib/PackageKit/transactions.db'
        'etc/PackageKit/pacman.d/pacman.conf'
        'etc/PackageKit/pacman.d/repos.list')
options=('!libtool')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2")
sha256sums=('35547a9b38eac8925a252ecba06e5809b7e6955d8009036134a2003d4e1ece70')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in
	
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-qt \
		--disable-managed \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-dummy \
		--enable-pacman \
		--with-default-backend=pacman
	
	make -s
	make -s DESTDIR="${pkgdir}" install
	
	# clean up unneeded stuff
	rm -rf "${pkgdir}/usr/share/PackageKit/website"
	
	# rename bash completion script
	mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"
	
	# make sure log gets removed
	touch "${pkgdir}/var/log/PackageKit"
}
