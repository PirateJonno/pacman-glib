# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=gnome-packagekit
pkgver=2.29.91
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('packagekit>=0.5.5' 'gconf>=2.28.0' 'gnome-icon-theme>=2.28.0'
         'dbus-glib>=0.73' 'libnotify>=0.4.3' 'libunique>=1.0.0'
         'libcanberra>=0.10' 'gnome-menus>=2.24.1' 'devicekit-power>=007')
makedepends=('gtk-doc' 'intltool')
install="${pkgname}.install"
source=("http://www.packagekit.org/releases/${pkgname}-${pkgver}.tar.gz"
        'gconf.patch'
        'gtk-2.28.patch'
        'install-local-files.patch'
        'hide-icon-blocked.patch'
        'disable-cancel-button.patch'
        'filters.patch')
md5sums=('f79a38ec3c1a0e759b9c7f455ff44310'
         '91608b91b46e322eea7f2b8f3c12d5dd'
         '2fac82791bcb7e01ed1d6b0dda54430b'
         '403783bdf7c1192e3cb60a8fb924f6ac'
         'd6b082732c2de096265399c0fa47016a'
         '80e4186bddac2de3620a6973ae2e1312'
         '004ae9c5cd063806b02407a77df7d32d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -Np1 -i "${srcdir}/gconf.patch" || return 1
	patch -Np1 -i "${srcdir}/gtk-2.28.patch" || return 1
	patch -Np1 -i "${srcdir}/install-local-files.patch" || return 1
	patch -Np1 -i "${srcdir}/hide-icon-blocked.patch" || return 1
	patch -Np1 -i "${srcdir}/disable-cancel-button.patch" || return 1
	patch -Np1 -i "${srcdir}/filters.patch" || return 1
	
	autoreconf || return 1
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gtk-doc \
		--disable-scrollkeeper || return 1
	
	make -s || return 1
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
	
	install -m 755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
	rm -rf "${pkgdir}/etc"
}
