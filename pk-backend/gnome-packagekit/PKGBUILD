# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=gnome-packagekit
pkgver=2.30.1
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('packagekit>=0.5.5' 'glib2>=2.18.0' 'gtk2>=2.19.3' 'gconf>=2.22.0'
         'dbus-glib>=0.73' 'libnotify>=0.4.3' 'libunique>=1.0.0'
         'gnome-menus>=2.24.1' 'upower>=0.9.1' 'libcanberra>=0.10')
makedepends=('gtk-doc' 'intltool')
install="${pkgname}.install"
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2"
        'arch.patch')
sha256sums=('02dd94dd064ffb342745622508def95df04e5fe4fe783329747ed2af9ee6c11d'
            '56f2288d95f9d05151570b752d7a9d2709d61f087ddf0eebd7509570f2d697a9')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -Np1 -i "${srcdir}/arch.patch" || return 1
	
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-gtk-doc \
		--disable-scrollkeeper || return 1
	
	make -s || return 1
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
	
	install -m 755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas || return 1
	rm -rf "${pkgdir}/etc"
}
